#library "WEAPON"
#include "zcommon.acs"
#import "ALPHLIB.acs"

Script "WeaponGlow" ENTER
{
	while(true)
	{
		for(int i = 0; i < NUM_WEAPS; i++)
		{
			if(StrIcmp(GetWeapon(), WEAP_CLASSNAMES[i]) == 0 || StrIcmp(GetWeapon(), WEAP_ALTCLASSNAMES[i]) == 0)
			{
				str glowclass = StrParam(s:WEAP_COLORS[i], s:"GlowSpawner");
				SpawnForced(glowclass, GetActorX(0), GetActorY(0), GetActorZ(0) + 2.0);
			}
		}
		delay(1);
	}
}
//For the Thermite Spitter's alt-fire.
Script "PlayerDeathByThermite" (void)
{
	int initid = ActivatorTID();
	SetActivator(0, AAPTR_TARGET);
	int blastid = UniqueTID(1, 0);
	SpawnProjectile(0, "ThermiteChainReact", 0, 0, 0, 0, blastid);
	SetActorPosition(blastid, GetActorX(initid), GetActorY(initid), GetActorZ(initid) + 16.0, false);
}
//Every weapon should execute this script when fired.
Script "OnWeaponFired" (void) 
{
	GiveInventory("EnergyRechargeCD", 105);
	for(int i = 0; i < NUM_WEAPS; i++)
	{
		if(StrIcmp(GetWeapon(), WEAP_CLASSNAMES[i]) == 0 && CheckInventory(WEAP_ENERGYNAMES[i]) <= 20 && CheckInventory("NoLowEnergySound") == 0)
		{
			LocalAmbientSound("General/LowEnergy", 96);
			GiveInventory("NoLowEnergySound", 1);
		}
	}
	//ACS_NamedExecuteAlways("CheckAssassinTactics", 0);
	
}
//Handles Alt-Fire cooldowns.
Script "OnWeaponAltFired" (void) 
{
	for(int i = 0; i < NUM_WEAPS; i++)
	{
		if(StrIcmp(GetWeapon(), WEAP_CLASSNAMES[i]) == 0 || StrIcmp(GetWeapon(), WEAP_ALTCLASSNAMES[i]) == 0)
		{
			GiveInventory(WEAP_COOLDOWNNAMES[i], GetAmmoCapacity(WEAP_COOLDOWNNAMES[i]));
		}
	}
	ACS_NamedExecuteAlways("OnWeaponFired", 0);
	
}
//For use with Beam Rifle Alt-Fire (for unlagging the detonation)
Script "CheckAltFirePressed" (void)
{
	//Sets the activator to the shooter of the pellet, instead of the pellet itself.
	SetActivator(0, AAPTR_TARGET);
	if(GetPlayerInput(-1, INPUT_BUTTONS) & BT_ALTATTACK)
	{
		SetResultValue(1);
	}
	else
	{
		SetResultValue(0);
	}
}
//For use with Particle Splitter Alt-Fire (for unlagging the detonation)
Script "CheckAltFireReleased" (void)
{
	//Sets the activator to the shooter of the pellet, instead of the pellet itself.
	SetActivator(0, AAPTR_TARGET);
	if((GetPlayerInput(-1, INPUT_OLDBUTTONS) & BT_ALTATTACK) && !(GetPlayerInput(-1, INPUT_BUTTONS) & BT_ALTATTACK))
	{
		SetResultValue(1);
	}
	else
	{
		SetResultValue(0);
	}
}
Script "ZoomSetup" (void)
{
	SetActionScript(0, BT_ZOOM, "WeaponZoomControl");
}
Script "ZoomSetupClient" (void) CLIENTSIDE
{
	SetActionScript(0, BT_ZOOM, "WeaponZoomControl");
}
Script "WeaponZoomControl" (int predicting, int justPressed, int justReleased, int buttons)
{
	if (!predicting)
	{
		if (justPressed)
		{
			SetNetworkReplicationFlags( NETREP_SKIPOWNER );
			SetPlayerWeaponZoomFactor(PlayerNumber(), GetCVAR("alph_zoomamount"));
			SetNetworkReplicationFlags( 0 );
		}
		else if (justReleased)
		{
			SetNetworkReplicationFlags( NETREP_SKIPOWNER );
			SetPlayerWeaponZoomFactor(PlayerNumber(), 1.0);
			SetNetworkReplicationFlags( 0 );
		}
	}
}