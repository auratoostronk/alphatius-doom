#library "player"
#include "zcommon.acs"
#import "ACTIONS.acs"
#import "ALPHLIB.acs"
#import "TRAVEL.acs"
#import "HUD.acs"

int targetdummyid = 900;

Script "PlayerEnter" ENTER
{
	ACS_NamedExecuteAlways("SetupPlayer", 0);
	Thing_ChangeTID(0, 1000 + PlayerNumber());
	ACS_NamedExecuteAlways("SBARActionFixer", 0);
	//ACS_NamedExecuteAlways("DisplayLoadoutHUD", 0);
	delay(1); //Why is this needed? I don't know.
	GiveInventory("Action1Charge", GetAmmoCapacity("Action1Charge"));
}

script "PlayerRespawn" RESPAWN
{
	ACS_NamedExecuteAlways("SetupPlayer", 0);
	Thing_ChangeTID(0, 1000 + PlayerNumber());
	ACS_NamedExecuteAlways("SBARActionFixer", 0);
	//ACS_NamedExecuteAlways("DisplayLoadoutHUD", 0 );
}
Script "SetupPlayer" (void)
{
	if(!(ClassifyActor(0) & ACTOR_BOT))
	{
		GiveInventory(GetCVARString("alph_startweapon"), 1);
		
		if(	!CheckInventory("PhotonCarbine") &&
			!CheckInventory("ParticleSplitter") &&
			!CheckInventory("TeslaSMG") &&
			!CheckInventory("C14") &&
			!CheckInventory("SparkCannon") &&
			/*!CheckInventory("RocketCannon") && */
			!CheckInventory("Flamespitter") && 
			!CheckInventory("BeamRifle"))
			{
				GiveInventory("PhotonCarbine", 1);
			}
			
		for(int j = 0; j < NUM_WEAPS; j++)
		{
			GiveInventory(WEAP_ENERGYNAMES[j], GetAmmoCapacity(WEAP_ENERGYNAMES[j]));
		}
		ACS_NamedExecuteAlways("RestoreActionTimers", 0);
		switch(GetCVAR("alph_travel"))
		{
			case 0:
			GiveInventory("TravelThrusterBoots", 1);
			GiveInventory("ThrusterBootsCharge", 3);
			GiveInventory("TBootsCD", GetAmmoCapacity("TBootsCD"));
			break;
			case 1:
			GiveInventory("TravelWarpDrive", 1);
			break;
			case 2:
			GiveInventory("TravelHyperdrive", 1);
			break;
			case 3:
			GiveInventory("TravelJumpThrust", 1);
			break;
			default:
			GiveInventory("TravelThrusterBoots", 1);
			GiveInventory("ThrusterBootsCharge", 3);
			GiveInventory("TBootsCD", GetAmmoCapacity("TBootsCD"));
			break;
		}
		switch(GetCVAR("alph_action"))
		{
			case 0:
			GiveInventory("ActionRegenZone", 1);
			break;
			case 1:
			GiveInventory("ActionAutoDrone", 1);
			break;
			case 2:
			GiveInventory("ActionPowerBrace", 1);
			break;
			case 3:
			GiveInventory("ActionDecoyHologram", 1);
			break;
			case 4:
			GiveInventory("ActionSonicShockwave", 1);
			break;
			default:
			GiveInventory("ActionRegenZone", 1);
			break;
		}
	}
}
Script "PlayerDeath" DEATH
{
	Thing_ChangeTID(0, 0);
}
Script "PlayerDisconnect" (int playerwholeft) DISCONNECT
{
	Thing_ChangeTID(0, 0);
}
Script "TargetDummySpawn" (void)
{
	Thing_ChangeTID(0, targetdummyid);
	targetdummyid++;
	if(targetdummyid >= 1000)
	{
		targetdummyid = 900;
	}
}