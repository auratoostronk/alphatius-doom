Actor AlphatiusPlayer : PlayerPawn
{
	Speed 1
	Health 100
	Radius 16
	Height 56
	Mass 100
	PainChance 255
	GibHealth 30
	Player.CrouchSprite "PLYC"
	Player.MaxHealth 100
	Player.MugshotMaxHealth -1
	Player.ForwardMove 1.8, 0.9
	Player.SideMove 2.0, 1.0
	Player.DisplayName "Alphatius Player"
	Player.ColorRange 112, 127
	Player.JumpZ 8.0
	Player.DamageScreenColor "DarkRed", 1.0
	Player.StartItem "C14RespawnProtection1"
	Player.StartItem "C14RespawnProtection2"
	Species "PlayerRelated"
	+TELESTOMP
	States
	{
	Spawn:
		PLAY A 2
		Loop
	See:
		PLAY AABBCCDD 2
		Loop
	Missile:
		PLAY EF 6 Bright
		Goto Spawn
	Melee:
		PLAY EF 3 BRIGHT
		Goto Missile
	Pain:
		TNT1 AA 0 A_SpawnItemEx("NewBlood", 0.0, 0.0, 32.0, frandom(-4.0, 4.0),frandom(-4.0, 4.0), frandom(-2.0, 4.0)) 
		PLAY G 3 Bright 
		PLAY G 3 Bright A_Pain
		Goto Spawn
	Pain.TeslaTaser:
		TNT1 A 0 A_GiveInventory("TaserProtection", 1)
		Goto Pain
	Pain.FlamespitterThermite:
		TNT1 A 0 A_GiveInventory("ThermiteDebuff", 105)
		Goto Pain
	Death.FlamespitterThermite:
		TNT1 A 0 ACS_NamedExecuteAlways("PlayerDeathByThermite",0)
		Goto Death
	XDeath.FlamespitterThermite:
		TNT1 A 0 ACS_NamedExecuteAlways("PlayerDeathByThermite",0)
		Goto XDeath
	Death.FlamespitterFlames:
		TNT1 A 0 A_JumpIfInventory("ThermiteDebuff", 1, "Death.FlamespitterThermite")
		Goto Death
	Death:
		TNT1 AAA 0 A_SpawnItemEx("NewBlood", 0.0, 0.0, 32.0, frandom(-4.0, 4.0),frandom(-4.0, 4.0), frandom(-2.0, 4.0)) 
		PLAY H 10
		PLAY I 10 A_PlayerScream
		PLAY J 10 A_NoBlocking
		PLAY KLM 10
		PLAY N -1
		stop
	XDeath.FlamespitterFlames:
		TNT1 A 0 A_JumpIfInventory("ThermiteDebuff", 1, "XDeath.FlamespitterThermite")
		Goto XDeath
	XDeath:
		TNT1 AAAAA 0 A_SpawnItemEx("NewBlood", 0.0, 0.0, 32.0, frandom(-4.0, 4.0),frandom(-4.0, 4.0), frandom(-2.0, 4.0)) 
		PLAY O 5
		PLAY P 5 A_PlayerScream
		PLAY Q 5 A_NoBlocking
		PLAY RSTUV 5
		PLAY W -1
		stop
	}
}
Actor NewBlood : Blood replaces Blood
{
	Scale 0.6
	Mass 6
	-ALLOWPARTICLES
	States
	{
	Spawn:
		HBLD ABCDEF 3 A_SetTics(random(1, 2))
		Stop
	}
}