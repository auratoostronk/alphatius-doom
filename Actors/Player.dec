Actor AlphatiusPlayer : PlayerPawn
{
	Speed 1
	Health 100
	Radius 16
	Height 56
	Mass 100
	PainChance 255
	GibHealth 30
	Player.CrouchSprite "PLYC"
	Player.MaxHealth 100
	Player.MugshotMaxHealth -1
	Player.ForwardMove 1.5, 0.75
	Player.SideMove 1.7, 0.85
	Player.DisplayName "Alphatius Player"
	Player.ColorRange 112, 127
	Player.JumpZ 8.0
	Player.DamageScreenColor "DarkRed", 1.0
	Player.StartItem "C14RespawnProtection1"
	Player.StartItem "C14RespawnProtection2"
	Species "PlayerRelated"
	DamageFactor "StickyPlasma", 0.5
	+TELESTOMP
	States
	{
	Spawn:
		PLAY A 2
		Loop
	See:
		PLAY AABBCCDD 2
		Loop
	Missile:
		PLAY EF 6 
		Goto Spawn
	Melee:
		PLAY EF 3 BRIGHT
		Goto Missile
	Pain:
		TNT1 A 0 ACS_NamedExecuteAlways("DamagePlayerPain", 0)
		TNT1 A 0 ACS_NamedExecuteAlways("PlayerPain", 0)
		PLAY G 3 Bright 
		PLAY G 3 Bright A_Pain
		Goto Spawn
	Pain.TeslaTaser:
		TNT1 A 0 A_GiveInventory("TaserProtection", 1)
		Goto Pain
	Death.SGDecay:
		TNT1 A 0 ACS_NamedExecuteAlways("PlayerDeathByDecayPellet",0)
		Goto Death
	XDeath.SGDecay:
		TNT1 A 0 ACS_NamedExecuteAlways("PlayerDeathByDecayPellet",0)
		Goto XDeath
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("DamagePlayerPain", 0)
		TNT1 AAA 0 A_SpawnItemEx("NewBlood", 0.0, 0.0, 32.0, frandom(-4.0, 4.0),frandom(-4.0, 4.0), frandom(-2.0, 4.0)) 
		PLAY G 2
		PLAY H 4 A_PlayerScream
		PLAY IJKLM 3
		PLAY N -1
		stop
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("DamagePlayerPain", 0)
		TNT1 AAAAA 0 A_SpawnItemEx("NewBlood", 0.0, 0.0, 32.0, frandom(-4.0, 4.0),frandom(-4.0, 4.0), frandom(-2.0, 4.0)) 
		PLAY O 5
		PLAY P 5 A_XScream
		PLAY Q 5 A_NoBlocking
		PLAY RSTUV 5
		PLAY W -1
		stop
	}
}
Actor NewBlood : Blood replaces Blood
{
	Mass 6
	-ALLOWPARTICLES
	States
	{
	Spawn:
		HBLD ABCDEF 3 A_SetTics(random(1, 2))
		Stop
	}
}