ACTOR C14 : Weapon 10005
{
	//$Category Alphatius Weapons
	Radius 32
	Tag "C14 Energy Blaster"
	Weapon.BobStyle "Smooth"
	Weapon.AmmoType1 "C14Energy"
	Weapon.AmmoType2 "C14Energy"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 5
	+WEAPON.NOAUTOAIM
	Inventory.Icon "C14PA0"
	Weapon.SlotNumber 6
	Weapon.SelectionOrder 500
	Inventory.RespawnTics 280 //8 seconds
	States
	{
	Spawn:
		C14P A 1 bright
		Loop
	Ready:
		C14R A 1 A_WeaponReady
		Loop
	Deselect:
		C14R AA 0 A_Lower
		C14R A 1 A_Lower
		Loop
	Select:
		C14R AA 0 A_Raise
		C14R A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_PlaySound("C14/Fire", CHAN_WEAPON)
		TNT1 A 0 A_SpawnItemEX("GreenLightShoot")
		C14F A 2 A_FireCustomMissile("C14Projectile", frandom(-0.2,0.2), true, 0, -5.0, 0, frandom(-0.2,0.2))
		C14F BC 2
		C14R A 2
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("C14AltCD",1,"AltFail")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponAltFired", 0)
		TNT1 A 0 A_SpawnItemEX("GreenLightShoot")
		TNT1 A 0 A_PlaySound("GrenadeLaunch", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("C14GasGrenade", frandom(-0.2,0.2), true, 0.0, -2.0, 0, frandom(-0.2,0.2))
		C14R A 8
		Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		C14R A 3 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
  }
}

Actor C14Projectile
{
	Radius 5
	Height 5
	Scale 0.5
	Obituary "%k > \cdC14\c- > %o"
	Projectile
	Speed 128
	+BRIGHT
	Damage (2)
	RenderStyle Add
	ProjectileKickback 80
	Decal BigScorch
	States 
	{
	Spawn:
		CPBL ABCDEFGHIJKL 1  A_SpawnItemEx("C14Trail")
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("C14Blast1")
		TNT1 AAAAAA 0 A_SpawnItemEx("C14Blast2", 0, 0, 0, frandom(-4, 4), frandom(-4, 4), frandom(2, 8))
		TNT1 A 0 A_Quake(2, 4, 0, 128)
		TNT1 A 0 A_Explode(18, 96)
		TNT1 A 0 A_Explode(4, 96, XF_HURTSOURCE, true, 96)
		TNT1 A 35 A_PlaySound("C14/Hit", CHAN_AUTO)
		Stop
	}
}
Actor C14Trail
 {
	Scale 0.05
	Alpha 0.6
	Renderstyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NONETID
	+FORCEXYBILLBOARD
	+BRIGHT
		
	States 
	{
	Spawn:
		TNT1 A 0 A_SetScale(scalex + 0.01, scaley + 0.01)
		CPEW A 1 A_FadeOut(0.05)
		loop
	}
}
Actor C14Blast1 
{
	Scale 0.8
	Alpha 1.0
	Renderstyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	
	+FORCEXYBILLBOARD
	+BRIGHT
	States
	{
	Spawn:
		CPEW A 1
		TNT1 A 0 A_SetScale(scalex * 1.1, scaley * 1.1)
		TNT1 A 0 A_FadeOut(0.1)
		Loop
			
	}
}
Actor C14Blast2
{
	RenderStyle ADD
	Scale 0.1
	Projectile
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+BOUNCEONACTORS
	+CLIENTSIDEONLY
	+NONETID
	BounceType Doom
	BounceFactor 0.5
	BounceCount 2
	+BRIGHT
	-NOGRAVITY
	Gravity 1.5
	States
	{
	Spawn:
		CPBL ABCDEFGHIJKL 1
		Loop
	Death:
		CPBL ABCDEFGHIJKL 1 A_FadeOut(0.1)
		stop	
	}
}
const int gas_radius = 192;
Actor C14GasGrenade
{
	Height 8
	Radius 8
	Projectile
	Speed 48
	Damage (10)
	Obituary "%k > \cdC14 (Alt Impact)\c- > %o"
	-NOGRAVITY
	+NOBLOOD
	+DONTSPLASH
	DamageType "C14Alt"
	States
	{
	Spawn:
		SGRN A 1
		loop
	Death:
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 0)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 30)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 60)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 90)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 120)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 150)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 180)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 210)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 240)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 270)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 300)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 330)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 15)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 45)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 75)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 105)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 135)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 165)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 195)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 225)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 255)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 285)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 315)
		TNT1 A 0 A_SpawnItemEx("C14GasDamageIndicator", gas_radius, 16, 0, 0, 0, 0, 345)
		SGRN A 30 A_PlaySound("GrenadeHit", CHAN_AUTO)
		TNT1 A 0 A_PlaySound("C14/GasHiss", CHAN_AUTO)
		TNT1 A 0 A_SpawnItemEX("C14GasDamage", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0)
		//TNT1 A 0 A_SpawnItemEX("C14GasSlow", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0)
		//TNT1 A 0 A_SpawnItemEX("C14GasTravelStopper", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0)
		SGRN AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 4 A_SpawnItemEX("C14GasCloud", 0.0, 0.1, 16.0, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(0.0, 2.0))
		SGRN A 350
		TNT1 A 1
		stop
		
	}
}
Actor C14GasCloud
{
	Alpha 0.8
	RenderStyle Add
	Radius 2
	Height 2
    +NOINTERACTION
	+CLIENTSIDEONLY
	+NONETID
	Speed 10
	Scale 2.0
	States
	{
	Spawn:
		SMKG BCDEFGHIJKLM 20 
	Death:
		TNT1 A 1
		stop
			
	}
}
Actor C14GasDamage
{
	//PoisonDamage 3, 15, 3
	+THRUACTORS
	+NOGRAVITY
	+NODAMAGETHRUST
	+DONTSPLASH
	+NONETID
	DamageType "ToxicGas"
	Obituary "%k > \cdC14 (Alt)\c- > %o"
	args 0, 0
	States
	{
	Spawn:
		TNT1 A 0 A_Explode(10, gas_radius, XF_HURTSOURCE, true, gas_radius)
		TNT1 A 0 A_SetArg(1, args[1] + 1)
		TNT1 A 4 A_JumpIf(args[1] > 35, "Death")
		loop
	Death:
		TNT1 A 1
		Stop
	}
}
Actor C14GasDamageIndicator
{
	+BRIGHT
    +NOINTERACTION
	+CLIENTSIDEONLY
	+NONETID
	Scale 0.1
	Renderstyle ADD
	States
	{
		Spawn:
		TNT1 A 0 nodelay A_Warp(AAPTR_DEFAULT, 0, 0, 0, 0, WARPF_TOFLOOR)
		CPBL ABCDEFGHIJKLABCDEFGHIJKLABCDEFGHIJKL 4
		CPBL ABCDEFGHIJKL 4 A_FadeOut(0.1)
		Goto Death
	}
}
Actor C14RespawnProtection1 : PowerProtection
{
	+NONETID
	DamageFactor "GammaBeam", 0.0
	Powerup.Duration -2
}
Actor C14RespawnProtection2 : PowerProtection
{
	+NONETID
	DamageFactor "ToxicGas", 0.0
	Powerup.Duration -2
}