ACTOR BeamRifle : Weapon replaces Shotgun
{
	//$Category Alphatius Weapons
	Radius 32
	Height 32
	Decal RailScorchLower
	Tag "(4) Beam Rifle"
	//For some reason this is needed :c
	Obituary "%k > \cnBeam Rifle\c- > %o"
    Weapon.BobRangeX 0.7
    Weapon.BobRangeY 1.0
    Weapon.BobSpeed 1.5
    Weapon.BobStyle "Alpha"  	
	Weapon.AmmoType1 "BeamEnergy"
	Weapon.AmmoType2 "BeamEnergy"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 2
	+WEAPON.NOAUTOAIM
	+BLOODLESSIMPACT
	+SERVERNETID
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 500
	Weapon.Kickback 200
	Inventory.Icon "BRFPA0"
	DamageType "BeamRifle"
	Inventory.RespawnTics 280 //8 seconds
	States
	{
	Spawn:
		BRFP A 1 bright A_SetAngle(angle + 1.0)
		Loop
	Ready:
		//TNT1 A 0 A_PlaySound("BeamRifle/FireStop", CHAN_WEAPON, 1.0, false) //These are here in case ammo runs out or the player dies while firing
		//TNT1 A 0 A_StopSound(CHAN_WEAPON)  									//These are here in case ammo runs out or the player dies while firing
		BRFR A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_PlaySound("BeamRifle/FireStop", CHAN_WEAPON, 1.0, false) //These are here in case ammo runs out or the player dies while firing
		TNT1 A 0 A_StopSound(CHAN_WEAPON)									//These are here in case ammo runs out or the player dies while firing
		BRFR A 1 A_Lower
		TNT1 AA 0 A_Lower
		Loop
	Select:
		BRFR A 1 A_Raise
		TNT1 AA 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_SpawnItemEX("LightBlueLightShoot")
		TNT1 A 0 A_PlaySound("BeamRifle/Fire", CHAN_WEAPON, 1.0, true)
		TNT1 A 0 A_Refire("Hold")
		Goto Ready
	Hold:
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		BRFF A 1 A_RailAttack(3, 2.5, true, "none", "none", RGF_SILENT, 0, "BeamRailHit", 0, 0, 640.0, 1.0, 8.0, 0, "BeamRailPuff", -1.0)
		BRFF A 1 A_RailAttack(4, 2.5, true, "none", "none", RGF_SILENT, 0, "BeamRailHit", 0, 0, 640.0, 1.0, 8.0, 0, "BeamRailPuff", -1.0)
		BRFF E 1 A_Refire("Hold")
		TNT1 A 0 A_PlaySound("BeamRifle/FireStop", CHAN_WEAPON, 1.0, false) //These are here in case ammo runs out or the player dies while firing
		TNT1 A 0 A_StopSound(CHAN_WEAPON)									//These are here in case ammo runs out or the player dies while firing
		
		BRFR A 6 A_WeaponReady
		Goto Ready
	AltFire:
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_SpawnItemEX("LightBlueLightShoot")
		TNT1 A 0 A_PlaySound("BeamRifle/AltFire", CHAN_WEAPON, 1.0, false)
		BRFF C 1 A_RailAttack(15, 2.5, true, "none", "none", RGF_SILENT, 0, "BeamRailHit2", 0, 0, 16384.0, 1.0, 16.0, 0, "BeamRailPuff2", -1.0)
		BRFF DEFG 1
		BRFF A 1
		Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		BRFR A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
  }
}
Actor BeamRailPuff
{
	Radius 2
	Height 2
	Scale 0.3
	Renderstyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NONETID
	+BRIGHT
	States
	{
	Spawn:
	    LSPK A 1 Bright A_FadeOut(0.25)
		Loop
	}
}
Actor BeamRailPuff2 : BeamRailPuff
{
	States
	{
	Spawn:
	    LSPK A 1 Bright A_FadeOut(0.1)
		Loop
	}
}
Actor BeamRailHit : BulletPuff
{
	+AlWAYSPUFF
	-ALLOWPARTICLES
	+DROPOFF
	+NOCLIP
	Alpha 1.0
	Vspeed 0
	Scale 0.4
	DamageType "BeamRifle"
	ProjectileKickback 100
	Obituary "%k > \cnBeam Rifle\c- > %o"
	+BRIGHT
	States
	{
	Spawn:
	    ZAP6 ABCDE 1 A_FadeOut(0.2)
		Stop
	}
}
Actor BeamRailHit2 : BeamRailHit
{
	DamageType "BeamRifleAlt"
	Obituary "%k > \cnBeam Rifle (Alt)\c- > %o"
	ProjectileKickback 200
}