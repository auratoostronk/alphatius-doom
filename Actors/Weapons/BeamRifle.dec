ACTOR BeamRifle : Weapon 10011
{
	//$Category Alphatius Weapons
	Radius 32
	Decal RailScorchLower
	Tag "Beam Rifle"
	//For some reason this is needed :c
	Obituary "%k > \cnBeam Rifle\c- > %o"
	Weapon.BobStyle "Smooth"
	Weapon.AmmoType1 "BeamEnergy"
	Weapon.AmmoType2 "BeamEnergy"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 2
	+WEAPON.NOAUTOAIM
	+BLOODLESSIMPACT
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 500
	Weapon.Kickback 200
	Inventory.Icon "BRFPA0"
	Inventory.RespawnTics 280 //8 seconds
	States
	{
	Spawn:
		BRFP A 1 bright
		Loop
	Ready:
		BRFR A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_PlaySound("BeamRifle/FireStop", CHAN_WEAPON, 1.0, false)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		BRFR A 1 A_Lower
		TNT1 AA 0 A_Lower
		Loop
	Select:
		BRFR A 1 A_Raise
		TNT1 AA 0 A_Raise
		Loop
	Fire:
		//TNT1 A 0 A_JumpIf(CallACS("HoldingGrenadeSphere") == 1, "GrenadeCooloff")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_SpawnItemEX("LightBlueLightShoot")
		TNT1 A 0 A_PlaySound("BeamRifle/Fire", CHAN_WEAPON, 1.0, true)
		BRFF AB 1 A_RailAttack(4, 0, true, "none", "none", RGF_SILENT, 0, "BeamRailHit", 0, 0, 640.0, 1.0, 8.0, 0, "BeamRailPuff", -2)
		TNT1 A 0 A_Refire("Fire")
		TNT1 A 0 A_PlaySound("BeamRifle/FireStop", CHAN_WEAPON, 1.0, false)
		BRFF E 1 A_StopSound(CHAN_WEAPON)
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("BeamAltCD", 1, "AltFail")
		//BRFF A 12 A_PlaySound("BeamRifle/AltCharge", CHAN_WEAPON, 1.0, false)
		//TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_SpawnItemEX("LightBlueLightShoot")
		TNT1 A 0 A_PlaySound("BeamRifle/AltFire", CHAN_WEAPON)
		//TNT1 A 0 A_FireCustomMissile("BeamCannonMissile", 0.0, true, 0.0, -2.0, 0, 0.0)
		BRFF A 1 A_RailAttack(14, 0, true, "none", "none", RGF_SILENT, 0, "BeamRailHit2", 0, 0, 16384.0, 1.0, 32.0, 0, "BeamRailPuff2", -2)
		BRFF BC 2
		BRFR A 1
		Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		BRFR A 3 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
  }
}
/*
Actor BeamCannonMissile : FastProjectile
{
	Projectile
	Radius 6
	Height 6
	Scale 2.0
	Speed 96
	Decal RailScorchLower
	Damage (0)
	Renderstyle Add
	Obituary "%k > \cnBeam Rifle (Alt)\c- > %o"
	+BRIGHT
	MissileType "BeamCannonTrail"
	MissileHeight 8
	States
	{
	Spawn:
		LSPK A 1 
		loop
	Death:
		TNT1 A 0 A_Explode(40, 256)
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash2",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItem("ExplodeSmokeRing")
		TNT1 A 0 A_SpawnItem("PhotonGrenadeDeathFX")
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PhotonGrenadeDeathDots",0,0,0,frandom(-8,8),frandom(-8,8),frandom(-8,8))
		TNT1 AAAAA 0 A_SpawnItemEx("ExplodeSmoke",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2))
		TNT1 AAAA 0 A_CustomMissile("ExplodeSmokeSpawner",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Quake(2,8,0, 384)
		Stop
	}
}
Actor BeamCannonTrail
{
	RenderStyle ADD
	Scale 1.0
	+NOINTERACTION
	+CLIENTSIDEONLY
	+BRIGHT
	States
	{
		Spawn:
		LSPK A 8
		LSPK A 1 A_FadeOut(0.1)
		Goto Spawn+1
	}
}*/
Actor BeamRailPuff
{
	Scale 0.3
	Renderstyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	+BRIGHT
	States
	{
	Spawn:
	    LSPK A 1 Bright A_FadeOut(0.5)
		Loop
	}
}
Actor BeamRailPuff2 : BeamRailPuff
{
	States
	{
	Spawn:
		LSPK A 1
		Goto Spawn1
	Spawn1:
	    LSPK A 1 Bright A_FadeOut(0.2)
		Loop
	}
}
Actor BeamRailHit : BulletPuff
{
	+PUFFONACTORS
	+AlWAYSPUFF
	-ALLOWPARTICLES
	+DROPOFF
	Alpha 1.0
	Vspeed 0
	Scale 0.4
	DamageType "BeamRifle"
	ProjectileKickback 100
	Obituary "%k > \cnBeam Rifle\c- > %o"
	+BRIGHT
	States
	{
	Spawn:
	Melee:
	Crash:
	    ZAP6 ABCDE 1 A_FadeOut(0.2)
		Stop
	}
}
Actor BeamRailHit2 : BeamRailHit
{
	DamageType "BeamRifleAlt"
	Obituary "%k > \cnBeam Rifle (Alt)\c- > %o"
	ProjectileKickback 200
}