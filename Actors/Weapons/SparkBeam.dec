ACTOR SparkBeam : Weapon 10009
{
	Radius 32
	Decal RailScorchLower
	Tag "Spark Cannon"
	Weapon.UpSound "General/WeaponSwitch"
	Weapon.BobSpeed 2.0
	Weapon.BobStyle "Smooth"
	Weapon.AmmoType1 "SparkEnergy"
	Weapon.AmmoType2 "SparkEnergy"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	+NEVERRESPAWN
	+WEAPON.NOAUTOAIM
	+BLOODLESSIMPACT
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 500
	Inventory.Icon "SPKPA0"
	Inventory.RespawnTics 280 //8 seconds
	States
	{
	Spawn:
		SPKP A 1 bright
		Loop
	Ready:
		SPKR AABBCC 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("WeaponDeselect", 0)
		TNT1 A 0 A_PlaySound("SparkCannon/FireStop", CHAN_WEAPON, 1.0, false)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 AA 0 A_Lower
		SPKR A 1 A_Lower
		TNT1 AA 0 A_Lower
		SPKR B 1 A_Lower
		TNT1 AA 0 A_Lower
		SPKR C 1 A_Lower
		Loop
	Select:
		TNT1 AA 0 A_Lower
		SPKR A 1 A_Lower
		TNT1 AA 0 A_Lower
		SPKR B 1 A_Lower
		TNT1 AA 0 A_Lower
		SPKR C 1 A_Lower
		Loop
	Fire:
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_SpawnItemEX("RedLightShoot")
		TNT1 A 0 A_PlaySound("SparkCannon/Fire", CHAN_WEAPON, 1.0, true)
		SPKF A 1 Bright A_RailAttack(4,0,true,"none","none",RGF_SILENT,0,"LaserSparkDot",0,0,640.0,1,8.0,0,"LaserSpark", -2)
		TNT1 A 0 A_Refire("Fire")
		SPKF E 1 A_PlaySound("SparkCannon/FireStop", CHAN_WEAPON, 1.0, false)
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("SparkAltCD", 1, "AltFail")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		SPKF ABCD 2 Bright //A_PlayWeaponSound("SparkCannon/Charge")
		TNT1 A 0 A_SpawnItemEX("RedLightShoot")
		TNT1 A 0 A_PlaySound("SparkCannon/AltFire", CHAN_WEAPON)
		TNT1 A 0 A_Recoil(8)
		SPKF E 1 Bright A_FireCustomMissile("SparkBlastBall", 0, true, 0, -3.0)
		TNT1 A 0 A_GiveInventory("SparkAltCD", 140)
		SPKR EABCABC 2
		Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		SPKR ABC 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	NoEnergy:
		TNT1 A 0 ACS_NamedExecuteAlways("WeaponNoEnergy")
		TNT1 A 0 A_PlaySound("SparkCannon/FireStop", CHAN_WEAPON, 1.0, false)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		SPKR ABC 1
		Goto Ready
	}
}
Actor LaserSpark
{
	Scale 0.3
	Renderstyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	+BRIGHT
	States
	{
	Spawn:
	    RSPK A 1 Bright A_FadeOut(0.5)
		Loop
	}
}
Actor LaserSparkDot : BulletPuff
{
	+PUFFONACTORS
	+AlWAYSPUFF
	//-RANDOMIZE
	-ALLOWPARTICLES
	+DONTSPLASH
	+NOCLIP
	Alpha 1.0
	Vspeed 0
	Scale 0.4
	DamageType "SparkCannon"
	ProjectileKickback 150
	Obituary "%o was killed by %k's \"laser beam\""
	+BRIGHT
	States
	{
	Spawn:
	Melee:
	Crash:
	    ZAP8 ABCDE 1 A_FadeOut(0.20)
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("SparkSpeedBoost", 5)
		ZAP8 ABCDE 1 A_FadeOut(0.20)
		Stop
	}
}
Actor SparkSpeedBoost : Inventory
{
	Inventory.MaxAmount 5
}
Actor SparkBlastBall
{
	Projectile
	Radius 8
	Height 16
	Scale 1.5
	Speed 80
	Decal PlasmaScorchLower
	Damage (0)
	ProjectileKickback 1800
	Obituary "%o was blasted away by %k"
	+BRIGHT
	States
	{
	Spawn:
		SPBA ABCABC 2
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash2",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItem("ExplodeSmokeRing")
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ExplodeSmoke",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2))
		TNT1 AAAAAAAA 0 A_CustomMissile("ExplodeSmokeSpawner",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Quake(2,8,0,384)
		TNT1 A 0 A_Explode(20, 256)
		SPBA DEFGHI 2
		TNT1 A 1
		stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("SparkEnergy", 50)
		Goto Death
	}
}