Actor TeslaSMG : Weapon 10006
{
	//$Category Alphatius Weapons
	Radius 32
	Height 32
	Decal PlasmaScorchLower
	Tag "(1) Tesla SMG"
	Obituary "%k > \ckTesla SMG (Alt)\c- > %o"
    Weapon.BobRangeX 0.7
    Weapon.BobRangeY 1.0
    Weapon.BobSpeed 1.5
    Weapon.BobStyle "Alpha"  	
	Weapon.AmmoType1 "SMGEnergy"
	Weapon.AmmoType2 "SMGEnergy"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 5
	+WEAPON.NOAUTOAIM
	+SERVERNETID
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 500
	Inventory.Icon "SMGPA0"
	Inventory.RespawnTics 280 //8 seconds
	States
	{
	Spawn:
		SMGP A 1 bright A_SetAngle(angle + 1.0)
		loop	
	Ready:
		SMGR A 1 A_WeaponReady 
		Loop
	Deselect:
		SMGR AA 0 A_Lower
		SMGR A 1 A_Lower
		Loop
	Select:
		SMGR AA 0 A_Raise
		SMGR A 1 A_Raise
		Loop
	Fire:
		//TNT1 A 0 A_JumpIf(CallACS("HoldingGrenadeSphere") == 1, "GrenadeCooloff")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		//TNT1 A 0 A_GiveInventory("TeslaSpeedBoost", 5)
		TNT1 A 0 A_SpawnItemEX("YellowLightShoot")
		TNT1 A 0 A_PlaySound("TeslaSMG/Fire", CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("TeslaSpeedBoost", 5)
		SMGF A 1 A_FireCustomMissile("SMGProjectile", afrandom(-0.3,0.3), true, 4.0, -1.0, 0, afrandom(-0.3,0.3))
		SMGF B 1
		SMGF C 1
		Goto Ready
    AltFire:
        TNT1 A 0 A_JumpIfInventory("SMGAltCD", 1, "AltFail")
		TNT1 A 0 A_JumpIfInventory("AltFireDeviceCD", 1, "AltFail")
        TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponAltFired", 0)
		SMGM ABCDE 2
        TNT1 A 0 A_SpawnItemEX("YellowLightShoot")
		TNT1 A 0 A_TakeInventory("SMGEnergy", 5) 			//For some reason this is neccessary even with the A_CustomPunch flags changed
		TNT1 A 0 A_Recoil(-20)
        SMGM F 1 bright A_CustomPunch(80, true, !CPF_USEAMMO, "TeslaPuff", 160) 
        SMGM GHFGHFGHFGH 1 bright A_CustomPunch(80, true, !CPF_USEAMMO, "TeslaNoPuff", 160) 
		SMGM EDCBA 2
        Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		SMGR A 3 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	}
}

//Used to increase the user's speed while the user is firing.
Actor TeslaSpeedBoost : Inventory { 
	+SERVERNETID
	Inventory.MaxAmount 5 }

Actor TaserProtection : PowerProtection
{
	+SERVERNETID
	DamageFactor "TeslaTaser", 0.0
	Powerup.Duration 12
}
Actor SMGProjectile
{
	Projectile
	Speed 48
	Damage (10)
	Radius 8
	Height 12
	Decal PlasmaScorchLower
	Scale 0.3
	+DONTSPLASH
	+BRIGHT
	+RANDOMIZE
	//SeeSound "TeslaSMG/Fire"
	Obituary "%k > \ckTesla SMG\c- > %o"
	States
	{
	Spawn:
		ZAP7 ABCDE 1
		loop
	Death:
		ZAP7 FGHIJKL 1 A_SetScale(scalex + 0.05, scaley + 0.05)
		TNT1 A 1
		Stop
	}
}
Actor TeslaPuff : BulletPuff
{
	Scale 0.3
	+ALWAYSPUFF
	+PUFFONACTORS
	+BRIGHT
	+NOBLOCKMAP
	DamageType "TeslaTaser"
	ActiveSound "TeslaSMG/AltFire"
	AttackSound "TeslaSMG/AltFire"
	SeeSound "TeslaSMG/AltFireHit"
	Obituary "%k > \ckTesla SMG (Alt)\c- > %o"
	States
	{
	Spawn:
		ZAP7 ABCDE 4
		Stop
	Melee:
		ZAP7 ABCDE 4
		Stop
	}
}
Actor TeslaNoPuff : TeslaPuff
{
	ActiveSound ""
	AttackSound ""
	SeeSound "TeslaSMG/AltFireHit"
	Obituary "%k > \ckTesla SMG (Alt)\c- > %o"
	States
	{
	Spawn:
		TNT1 A 4
		Stop
	Melee:
		TNT1 A 4
		Stop
	}
}