ACTOR PhotonCarbine : Weapon 10004
{
	//$Category Alphatius Weapons
	Radius 32
	Tag "Photon Carbine"
	Obituary "%o was gunned down by %k"
	Weapon.AmmoType1 "CarbineEnergy"
	Weapon.AmmoType2 "CarbineEnergy"
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 20
	+WEAPON.NOAUTOAIM
	Inventory.Icon "CARPA0"
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 500
	Inventory.RespawnTics 280 //8 seconds
	States
	{
	Spawn:
		CARP A 1 bright
		Loop
	Ready:
		CARR A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("WeaponDeselect", 0)
		CARR AA 0 A_Lower
		CARR A 1 A_Lower
		Loop
	Select:
		CARR AA 0 A_Raise
		CARR A 1 A_Raise
		Loop
	Fire:
		//TNT1 A 0 A_JumpIf(CallACS("HoldingGrenadeSphere") == 1, "GrenadeCooloff")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_SpawnItemEX("BlueLightShoot")
		TNT1 A 0 A_PlaySound("PhotonCarbine/Fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("PhotonProjectile", frandom(-0.2, 0.2), true, 0.0, -2.0, 0 , frandom(-0.2, 0.2))
		CARF AB 2 Bright
		CARF C 1 A_Refire("Fire")
		CARR A 1
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("CarbineAltCD",1,"AltFail")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_SpawnItemEX("BlueLightShoot")
		TNT1 A 0 A_PlaySound("GrenadeLaunch", CHAN_WEAPON)
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_FireCustomMissile("PhotonGrenade",0.0,true,0,-2,0,3)
		TNT1 A 0 A_GiveInventory("CarbineAltCD", 35*5)
		CARF ABC 2
		Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		CARR A 3 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	NoEnergy:
		CARR A 3 ACS_NamedExecuteAlways("WeaponNoEnergy")
		Goto Ready
	GrenadeCooloff:
		CARR A 1
		TNT1 A 0 A_Refire("GrenadeCooloff")
		Goto Ready
  }
}
Actor PhotonProjectile : FastProjectile
{
	Projectile
	Radius 6
	Height 6
	Scale 1.5
	Speed 224
	Decal RailScorchLower
	Damage (15)
	Renderstyle Add
	Obituary "%o was gunned down by %k"
	+BRIGHT
	MissileType "PhotonTrail"
	MissileHeight 8
	States
	{
	Spawn:
		BSPK A 1 
		loop
	Death:
		TNT1 A 0 A_FaceTarget
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("PhotonDeathFX", 0.0, 0.0, 0.0, frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom (-4.0, 4.0))
		TNT1 A 35 A_PlaySound("PhotonCarbine/Hit", CHAN_AUTO, 0.8)
		Stop
	}
}
Actor PhotonTrail
{
	RenderStyle ADD
	+NOINTERACTION
	+CLIENTSIDEONLY
	+BRIGHT
	Scale 0.6
	States
	{
		Spawn:
		TNT1 A 0 A_SetScale(scalex * 0.8, scaley * 0.8)
		BSPK A 1 A_FadeOut(0.2)
		loop
	}
}
Actor PhotonDeathFX
{
	RenderStyle ADD
	Scale 0.8
	Gravity 0.5
	Speed 16
	-SOLID
	+NOCLIP
	+NOBLOCKMAP
	+BRIGHT
	States
	{
		Spawn:
		TNT1 A 0 A_SetScale(scalex - 0.1, scaley - 0.1)
		BSPK A 1 A_FadeOut(0.1)
		loop
	}
}
Actor PhotonGrenade
{
	Radius 3
	Height 3
	Scale 0.6
	Speed 48
	Gravity 1.0
	Damage (0)
	Decal BigScorch
	Projectile
	ProjectileKickBack 120
	RenderStyle Add
	Obituary "%o was noob tubed by %k"
	-NOGRAVITY
	+BRIGHT
	args 0
	States
	{
	Spawn:
		BGRN A 2 A_SpawnItemEx("PhotonGrenadeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		loop
	Death:
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash2",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItem("ExplodeSmokeRing")
		TNT1 A 0 A_SpawnItem("PhotonGrenadeDeathFX")
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PhotonGrenadeDeathDots",0,0,0,frandom(-8,8),frandom(-8,8),frandom(4,16))
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ExplodeSmoke",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2))
		TNT1 AAAAAAAA 0 A_CustomMissile("ExplodeSmokeSpawner",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Quake(2,8,0, 384)
		TNT1 A 0 A_Explode(65, 320)
		stop
	}
}

Actor PhotonGrenadeTrail
{
	Radius 2
	Height 2
	+NOINTERACTION
	+BRIGHT
	Scale 0.12
	RenderStyle Add
	States
	{
	Spawn:
		BGTR A 6
	Looped:
		BGTR A 1 A_Fadeout(0.03)
		loop
	}
}
Actor PhotonGrenadeDeathFX
{
	RenderStyle ADD
	Scale 0.8
	+CLIENTSIDEONLY
	+NOINTERACTION
	+BRIGHT
	States
	{
		Spawn:
		TNT1 A 0 A_SetScale(scalex * 1.2, scaley  * 1.2)
		BGTR A 1 A_FadeOut(0.1)
		loop
	}
}
Actor PhotonGrenadeDeathDots
{
	RenderStyle ADD
	Scale 1.0
	Projectile
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+BOUNCEONACTORS
	BounceType Doom
	BounceFactor 0.6
	BounceCount 3
	+BRIGHT
	-NOGRAVITY
	Gravity 1.0
	States
	{
		Spawn:
		BSPK A 1
		loop
		Death:
		BSPK A 35
		BSPK AAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.05)
		TNT1 A 1 
		stop
	}
}