ACTOR SparkCannon : Weapon 10009
{
	//$Category Alphatius Weapons
	Radius 32
	Decal RailScorchLower
	Tag "Spark Cannon"
	//For some reason this is needed :c
	Obituary "%k > \cgSpark Cannon\c- > %o"
	Weapon.BobStyle "Smooth"
	Weapon.AmmoType1 "SparkEnergy"
	Weapon.AmmoType2 "SparkEnergy"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	+WEAPON.NOAUTOAIM
	+BLOODLESSIMPACT
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 500
	Inventory.Icon "SPKPA0"
	Inventory.RespawnTics 280 //8 seconds
	States
	{
	Spawn:
		SPKP A 1 bright
		Loop
	Ready:
		SPKR AABBCC 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 AA 0 A_Lower
		SPKR A 1 A_Lower
		TNT1 AA 0 A_Lower
		SPKR B 1 A_Lower
		TNT1 AA 0 A_Lower
		SPKR C 1 A_Lower
		Loop
	Select:
		TNT1 AA 0 A_Raise
		SPKR A 1 A_Raise
		TNT1 AA 0 A_Raise
		SPKR B 1 A_Raise
		TNT1 AA 0 A_Raise
		SPKR C 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_SpawnItemEX("RedLightShoot")
		TNT1 A 0 A_PlaySound("SparkCannon/Fire", CHAN_WEAPON)
		TNT1 A 0 A_Recoil(6)
		TNT1 A 0 A_RailAttack(75, 0, true, "none", "none", RGF_SILENT, 0.0, "LaserSparkDot", 0, 0, 16384.0, 2, 4.0, 0, "LaserSpark", -2)
		SPKF D 4 A_RailAttack(0, 0, false, "none", "none", RGF_SILENT, 0.0, "LaserSparkDot", 0, 0, 16384.0, 2, 128.0, 0, "LaserSpark2", -2)
		SPKF E 21
		SPKF E 35 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("SparkAltCD", 1, "AltFail")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponAltFired", 0)
		TNT1 A 0 A_SpawnItemEX("RedLightShoot")
		TNT1 A 0 A_PlaySound("SparkCannon/AltFire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("FluxOrbMissile", 0, true, 0, -0.5, 0, 0)
		SPKF D 4
		SPKF E 16
		Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		SPKR AABBCC 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
  }
}
Actor LaserSpark
{
	Radius 2
	Height 2
	Scale 0.3
	Renderstyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NONETID
	+BRIGHT
	States
	{
	Spawn:
	    RSPK A 5
	Spawn1:
		RSPK A 1 A_FadeOut(0.05)
		Loop
	}
}
Actor LaserSpark2 : LaserSpark
{
	Scale 0.05
	States
	{
	Spawn:
		TNT1 A 0 A_SetScale(scalex + 0.01, scaley + 0.01)
	    RPEW A 1 Bright A_FadeOut(0.05)
		Loop
	}
}
Actor LaserSparkDot : BulletPuff
{
	+AlWAYSPUFF
	//-RANDOMIZE
	-ALLOWPARTICLES
	-NOINTERACTION
	+NOBLOCKMAP
	+DONTSPLASH
	+NOCLIP
	+DROPOFF
	Alpha 1.0
	Vspeed 0
	Scale 0.4
	DamageType "SparkRail"
	Obituary "%k > \cgSpark Cannon\c- > %o"
	ProjectileKickback 100
	+BRIGHT
	States
	{
	Spawn:
	    ZAP8 ABCDE 2 nodelay A_FadeOut(0.1)
		loop
	}
}
Actor FluxOrbMissile : UnlaggedActor
{
	RenderStyle add
	Speed 40
	Radius 12
	Height 24
	Damage (0)  
	Scale 1.6
	Health 10000
	Mass 0xfffffd
	Projectile
	ProjectileKickback 160
	Obituary "%k > \cgSpark Cannon (Alt)\c- > %o"
	+DONTSPLASH
	-NOBLOCKMAP
	+SHOOTABLE
	DamageFactor "Normal", 0.0
	DamageFactor "SparkRail", 10000.0
	+BRIGHT
	Decal PlasmaScorchLower
	States
	{
	Spawn:
		SPBA ABC 2
		loop
	RailedDeath:
		TNT1 A 0 A_SetScale(scalex * 1.5, scaley * 1.5)
		TNT1 A 0 A_Explode(150, 384)
		TNT1 A 0 A_SpawnItemEx("FluxOrbFXSpawner2", 0, 0, 0, 0, 0, 0, 0, SXF_NOUNLAGGED)
		DMFX DEFGHI 2
		TNT1 A 35 
		stop
		
	Death:
		TNT1 A 0 A_JumpIf(health < -1000, "RailedDeath")  
		TNT1 A 0 A_Explode(40, 256)
		TNT1 A 0 A_SpawnItemEx("FluxOrbFXSpawner", 0, 0, 0, 0, 0, 0, 0, SXF_NOUNLAGGED)
		TNT1 A 0 A_SetInvulnerable
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		SPBA DEFGHI 2
		TNT1 A 350
		stop
		
	}
}

Actor FluxOrbFXSpawner
{
	//+NOINTERACTION
	//+NONETID
	+SERVERNETID
	States
	{
	Spawn:
		TNT1 A 0 nodelay A_SpawnItemEx("ExplodeHitFlash")
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash2")
		TNT1 AAAAA 0 A_SpawnItemEx("ExplodeSmoke",0,0,0,frandom(-4,4),frandom(-4,4),frandom(-4,4))
		TNT1 AAAA 0 A_CustomMissile("ExplodeSmokeSpawner",0,0,frandom(0,360),4,frandom(0,360))
		TNT1 A 0 A_SpawnItem("ExplodeSmokeRing")
		Stop
	}
}

Actor FluxOrbFXSpawner2
{
	//+NOINTERACTION
	//+NONETID
	+SERVERNETID
	States
	{
	Spawn:
		TNT1 AAAAAAAAAA 0 nodelay A_SpawnItemEx("FluxOrbFX", 0, 0, 0, frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(-16.0, 16.0))
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash")
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash2")
		TNT1 AAAAA 0 A_SpawnItemEx("ExplodeSmoke",0,0,0,frandom(-4,4),frandom(-4,4),frandom(-4,4))
		TNT1 AAAA 0 A_CustomMissile("ExplodeSmokeSpawner",0,0,frandom(0,360),4,frandom(0,360))
		TNT1 A 0 A_SpawnItem("ExplodeSmokeRing")
		Stop
	}
}
Actor FluxOrbFX
{
	RenderStyle ADD
	Scale 1.0
	Projectile
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+BOUNCEONACTORS
	+CLIENTSIDEONLY
	+NONETID
	BounceType Doom
	BounceFactor 0.6
	BounceCount 3
	+BRIGHT
	-NOGRAVITY
	Gravity 1.0
	States
	{
		Spawn:
		RSPK A 1
		loop
		Death:
		RSPK A 35
		RSPK AAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.1)
		TNT1 A 1 
		stop
	}
}