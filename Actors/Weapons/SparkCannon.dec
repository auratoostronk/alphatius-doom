ACTOR SparkCannon : Weapon 10009
{
	//$Category Alphatius Weapons
	Radius 32
	Decal RailScorchLower
	Tag "Spark Cannon"
	//For some reason this is needed :c
	Obituary "%k > \cgSpark Cannon\c- > %o"
	Weapon.BobStyle "Smooth"
	Weapon.AmmoType1 "SparkEnergy"
	Weapon.AmmoType2 "SparkEnergy"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	+WEAPON.NOAUTOAIM
	+BLOODLESSIMPACT
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 500
	Inventory.Icon "SPKPA0"
	Inventory.RespawnTics 280 //8 seconds
	States
	{
	Spawn:
		SPKP A 1 bright
		Loop
	Ready:
		SPKR AABBCC 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 AA 0 A_Lower
		SPKR A 1 A_Lower
		TNT1 AA 0 A_Lower
		SPKR B 1 A_Lower
		TNT1 AA 0 A_Lower
		SPKR C 1 A_Lower
		Loop
	Select:
		TNT1 AA 0 A_Raise
		SPKR A 1 A_Raise
		TNT1 AA 0 A_Raise
		SPKR B 1 A_Raise
		TNT1 AA 0 A_Raise
		SPKR C 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_SpawnItemEX("RedLightShoot")
		TNT1 A 0 A_PlaySound("SparkCannon/Fire", CHAN_WEAPON)
		TNT1 A 0 A_Recoil(8)
		SPKF D 4 A_RailAttack(80, 0, true, "none", "none", RGF_SILENT, 0.0, "LaserSparkDot", 0, 0, 16384.0, 2, 4.0, 0, "LaserSpark2", -2)
		SPKF E 44
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("SparkAltCD", 1, "AltFail")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponAltFired", 0)
		TNT1 A 0 A_SpawnItemEX("RedLightShoot")
		TNT1 A 0 A_PlaySound("SparkCannon/AltFire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("FluxOrbMissile", 0, true, 0, -2.0, 0, 0)
		SPKF D 4
		SPKF E 12
		Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		SPKR AABBCC 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
  }
}
Actor LaserSpark
{
	Scale 0.3
	Renderstyle Add
	+NONETID
	+NOINTERACTION
	+CLIENTSIDEONLY
	+BRIGHT
	States
	{
	Spawn:
	    RSPK A 1 Bright A_FadeOut(0.5)
		Loop
	}
}
Actor LaserSpark2 : LaserSpark
{
	States
	{
	Spawn:
		RSPK A 3
		Goto Spawn1
	Spawn1:
	    RSPK A 1 Bright A_FadeOut(0.05)
		Loop
	}
}
Actor LaserSparkDot : BulletPuff
{
	+NONETID
	+PUFFONACTORS
	+AlWAYSPUFF
	//-RANDOMIZE
	-ALLOWPARTICLES
	+DONTSPLASH
	+NOCLIP
	Alpha 1.0
	Vspeed 0
	Scale 0.4
	DamageType "SparkRail"
	Obituary "%k > \cgSpark Cannon\c- > %o"
	ProjectileKickback 100
	+BRIGHT
	States
	{
	Spawn:
	Melee:
	Crash:
	    ZAP8 ABCDE 1 A_FadeOut(0.2)
		Stop
	}
}
Actor FluxOrbMissile
{
	RenderStyle add
	Speed 48
	Radius 12
	Height 24
	Damage (0)  
	Scale 1.6
	Health 10000
	Mass 0xfffffd
	Projectile
	ProjectileKickback 160
	Obituary "%k > \cgSpark Cannon (Alt)\c- > %o"
	+DONTSPLASH
	-NOBLOCKMAP
	+SHOOTABLE
	DamageFactor "Normal", 0.0
	DamageFactor "SparkRail", 10000.0
	+BRIGHT
	Decal PlasmaScorchLower
	States
	{
	Spawn:
		SPBA ABC 2
		loop
	RailedDeath:
		TNT1 A 0 A_SetScale(scalex * 1.5, scaley * 1.5)
		TNT1 A 0 A_Explode(150, 256)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("FluxOrbFX", 0, 0, 0, frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(-16.0, 16.0))
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash")
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash2")
		TNT1 A 0 A_SpawnItem("ExplodeSmokeRing")
		TNT1 AAAAA 0 A_SpawnItemEx("ExplodeSmoke",0,0,0,frandom(-4,4),frandom(-4,4),frandom(-4,4))
		TNT1 AAAA 0 A_CustomMissile("ExplodeSmokeSpawner",0,0,random(0,360),4,random(0,360))
		DMFX DEFGHI 2
		TNT1 A 35 
		stop
		
	Death:
		TNT1 A 0 A_JumpIf(health < -20, "RailedDeath")  
		TNT1 A 0 A_Explode(40, 192)
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash")
		TNT1 A 0 A_SpawnItemEx("ExplodeHitFlash2")
		TNT1 A 0 A_SpawnItem("ExplodeSmokeRing")
		TNT1 AAAAA 0 A_SpawnItemEx("ExplodeSmoke",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2))
		TNT1 AAAA 0 A_CustomMissile("ExplodeSmokeSpawner",0,0,random(0,360),2,random(0,360))
		DMFX DEFGHI 2
		TNT1 A 35 
		stop
		
	}
}

Actor FluxOrbFX
{
	RenderStyle ADD
	Scale 1.0
	Projectile
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+BOUNCEONACTORS
	+NONETID
	+CLIENTSIDEONLY
	BounceType Doom
	BounceFactor 0.6
	BounceCount 3
	+BRIGHT
	-NOGRAVITY
	Gravity 1.0
	States
	{
		Spawn:
		RSPK A 1
		loop
		Death:
		RSPK A 35
		RSPK AAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.1)
		TNT1 A 1 
		stop
	}
}