//Shotgun pellet spread multipliers.
const float spread1 = 1.0;
const float spread2 = 2.0;
const float spread3 = 3.0;

Actor ParticleSplitter : Weapon 10007
{
	//$Category Alphatius Weapons
	Radius 32
	Height 32
	Tag "(3) Particle Splitter"
	Weapon.BobStyle "Smooth"
	Weapon.AmmoType1 "PumpSGEnergy"
	Weapon.AmmoType2 "PumpSGEnergy"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	+WEAPON.NOAUTOAIM
	Inventory.Icon "PSGPA0"
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 500
	Inventory.RespawnTics 280 //8 seconds
	args 0
	States
	{
	Spawn:
		PSGP A 1 bright A_SetAngle(angle + 1.0)
		loop	
	Ready:
		PSGR A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 AA 0 A_Lower
		PSGR A 1 A_Lower
		Loop
	Select:
		TNT1 AA 0 A_Raise
		PSGR A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		TNT1 A 0 A_FireCustomMissile("PurpleLightShoot", 0, false)
        TNT1 A 0 A_SetArg(0, 0)
        TNT1 A 0 A_FireCustomMissile("SGPellet", 0.0, true, 0.0, -1.0, 0, 0.0)
		Goto Fire1
		//FIXED IT WOOT
		//Fires a ring of 6 inner projectiles.
    Fire1:
        TNT1 A 0 A_FireCustomMissile("SGPellet", spread1*cos(args[0] * 60), false, 3.0, -1.0, 0, spread1*sin(args[0] * 60))
        TNT1 A 0 A_SetArg(0, args[0] + 1)
        TNT1 A 0 A_JumpIf(args[0] < 6, "Fire1")
        TNT1 A 0 A_SetArg(0, 0)
        Goto Fire2
		//Fires 2 rings of 12 outer projectiles.
    Fire2:
        TNT1 A 0 A_FireCustomMissile("SGPellet", spread2*cos(args[0] * 30), false, 3.0, -1.0, 0, spread2*sin(args[0] * 30))
        TNT1 A 0 A_FireCustomMissile("SGPellet", spread3*cos(args[0] * 30), false, 3.0, -1.0, 0, spread3*sin(args[0] * 30))
        TNT1 A 0 A_SetArg(0, args[0] + 1)
        TNT1 A 0 A_JumpIf(args[0] < 12, "Fire2")
        Goto FireAnim
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PumpSGAltCD", 1, "AltFail")
		TNT1 A 0 A_JumpIfInventory("AltFireDeviceCD", 1, "AltFail")
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponAltFired", 0)
		TNT1 A 0 A_SpawnItemEX("PurpleLightShoot", 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile("SGAltPellet", 0, true, 3.0, -1.0, 0, 0.0)
		Goto FireAnim
	FireAnim:
		TNT1 A 0 A_PlayWeaponSound("ParticleSplitter/Fire")
		TNT1 A 0 A_Recoil(4.0)
		PSGF ABC 3
		PSGR A 6
		TNT1 A 0 A_PlaySound("ParticleSplitter/Pump1", CHAN_WEAPON)
		PSGF DEFGHIJKL 1
		TNT1 A 0 ACS_NamedExecuteAlways("OnWeaponFired", 0)
		PSGF MMMMMM 1 A_SpawnItemEX("SparkCannonExhaustSpawner", 8.0, 0.0, 32.0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0))
		TNT1 A 0 A_PlaySound("ParticleSplitter/Pump2", CHAN_WEAPON)
		PSGF NOPQRSTUV 1
		PSGR A 5
		Goto Ready
	AltFail:
		TNT1 A 0 ACS_NamedExecuteAlways("AltFireFail")
		PSGR A 3 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	}
}

Actor SGPellet
{
	Projectile
	RenderStyle add
	Speed 72
	Radius 2
	Height 2
	Damage (4)
	DamageType "ParticleSplitter"
	Decal RailScorchLower
	Scale 0.5
	Obituary "%k > \ctParticle Splitter\c- > %o"
	+DONTSPLASH
	+BRIGHT
	States
	{
	Spawn:
		PSPK AAAAAA 2 A_SetScale(scalex * 0.8, scaley * 0.8)
		//TNT1 AAAAAA 2
		Goto Death
	Death:
		TNT1 A 1 
		stop
	}
}/*
Actor SGPelletServerVisual : SGPellet
{
	Damage (0)
	States
	{
	Spawn:
		PSPK AAAAAA 2 A_SetScale(scalex * 0.8, scaley * 0.8)
		Goto Death
	Death:
		TNT1 A 1 
		stop
	}
}
Actor SGPelletClientVisual : SGPellet
{
	+CLIENTSIDEONLY
	Damage (0)
	States
	{
	Spawn:
		PSPK AAAAAA 2 A_SetScale(scalex * 0.8, scaley * 0.8)
		Goto Death
	Death:
		TNT1 A 1 
		stop
	}
}*/
Actor SGAltPellet : SGPellet
{
	Speed 48
	DamageType "ParticleSplitterAlt"
	Obituary "%k > \ctParticle Splitter (Alt Pre-detonation)\c- > %o"
	Scale 1.2
	args 0
	States
	{
	Spawn:
		PSPK A 1 nodelay A_JumpIf(ACS_NamedExecuteWithResult("CheckAltFireReleased") > 0, "Split")
		loop
	Split:
		TNT1 A 0 A_PlaySound("ParticleSplitter/Fire")
        TNT1 A 0 A_SetArg(0, 0)
		//Fires a ring of 6 inner projectiles
	Split1:
		TNT1 A 0 A_CustomMissile("SGAltPellet2", 0.0, 0.0, (spread1 * cos(args[0] * 60))-180, CMF_OFFSETPITCH|CMF_TRACKOWNER|CMF_SAVEPITCH,	(spread1 * sin(args[0] * 60))-180)
        TNT1 A 0 A_SetArg(0, args[0] + 1)
        TNT1 A 0 A_JumpIf(args[0] < 6, "Split1")
        TNT1 A 0 A_SetArg(0, 0)
        Goto Split2
		//Fires 3 rings of 12 outer projectiles
	Split2:
		TNT1 A 0 A_CustomMissile("SGAltPellet2", 0.0, 0.0, (spread2 * cos(args[0] * 30))-180, CMF_OFFSETPITCH|CMF_TRACKOWNER|CMF_SAVEPITCH,	(spread2 * sin(args[0] * 30))-180)
		TNT1 A 0 A_CustomMissile("SGAltPellet2", 0.0, 0.0, (spread3 * cos(args[0] * 30))-180, CMF_OFFSETPITCH|CMF_TRACKOWNER|CMF_SAVEPITCH,	(spread3 * sin(args[0] * 30))-180)
		//TNT1 A 0 A_CustomMissile("SGAltPellet2", 0.0, 0.0, (spread4 * cos(args[0] * 30))-180, CMF_OFFSETPITCH|CMF_TRACKOWNER|CMF_SAVEPITCH,	(spread4 * sin(args[0] * 30))-180)
        TNT1 A 0 A_SetArg(0, args[0] + 1)
        TNT1 A 0 A_JumpIf(args[0] < 12, "Split2")
        TNT1 A 0 A_SetArg(0, 0)
        Goto Death
	Death:
		TNT1 A 1 
		stop
	}
}
Actor SGAltPellet2 : SGPellet
{
	DamageType "ParticleSplitterAlt"
	Obituary "%k > \ctParticle Splitter (Alt)\c- > %o"
	States
	{
	Spawn:
		PSPK AAAAAA 2 A_SetScale(scalex * 0.8, scaley * 0.8)
		Goto Death
	Death:
		TNT1 A 1 
		stop
	}
}
//Purple
Actor SGPelletFX
{
	RenderStyle Add
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NONETID
	+BRIGHT
	Scale 0.4
	States
	{
	Spawn:
		PSPK A 1 A_fadeout(0.1)
		loop
	}
}