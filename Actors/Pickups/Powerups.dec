//This allows players to use their action ability an extra time.
//See "TRAVEL.acs" for implementation.
Actor ExtraTravelCharge : Ammo 12000
{ 
	//$Category Alphatius Pickups
	Radius 24
	Height 48
	Scale 0.5
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackMaxAmount 1
	Inventory.PickupMessage "Extra Travel Ability usage granted!"
	Inventory.RespawnTics 350 //10 seconds
	+FLOATBOB
	+BRIGHT
	+SERVERNETID
	States
	{
		Spawn:
		TCHG ABCDCBABCD 4
		loop
	}
}
//This is consumed to refill a portion of the player's weapon energy supply after the first instance their weapon energy runs out.
//See ALPHLIB.acs for implementation
Actor SpareAmmoPack : Ammo 12001
{
	//$Category Alphatius Pickups
	Radius 24
	Height 40
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackMaxAmount 1
	Inventory.PickupMessage "Spare Ammo Pack obtained. Fire away!"
	Inventory.RespawnTics 700 //20 seconds
	+FLOATBOB
	+BRIGHT
	+SERVERNETID
	States
	{
		Spawn:
		ENRG A 1 A_SetAngle(angle + 4)
		loop
	}
}
//Boosts the player's first 0.5 seconds of damage when firing a weapon.
Actor PowerCell : CustomInventory 12002
{
	//$Category Alphatius Pickups
	Radius 24
	Height 48
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.PickupMessage "Power Cell obtained. 25% damage until you shoot!"
	Inventory.RespawnTics 525 //15 seconds
	+FLOATBOB
	+BRIGHT
	+SERVERNETID
	States
	{
		Spawn:
		POWR A 1 nodelay A_SetAngle(angle + 4)
		loop
		Pickup:
		TNT1 A 0 A_GiveInventory("PowerCellDamageBoost", 1)
		stop
	}
}

Actor PowerCellDamageBoost : PowerDamage
{
	Powerup.Duration -3000000
	DamageFactor "Normal", 1.25
	DamageFactor "Ability", 1.00
	Powerup.Color "40 10 00"
}
Actor PowerCellDamageBoostExpiring : PowerDamage
{
	Powerup.Duration -2
	DamageFactor "Normal", 1.25
	DamageFactor "Ability", 1.00
	Powerup.Color "40 10 00"
}

Actor AlphatiusPowerup : CustomInventory
{
	//$Category Alphatius Pickups
	Radius 24
	Height 48
	Inventory.RespawnTics 4200 //120 seconds
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.Icon "CRONA0"
	Inventory.PickupMessage "Chronal Drive Aquired!"
	Inventory.PickupSound "Pickups/NanoArmor"
	+FLOATBOB
	+BRIGHT
	+SERVERNETID
	States
	{
		Spawn:
		TNT1 A 0 nodelay A_JumpIf(CallACS("CheckPowerupCVAR", 0), "Death")
		//TNT1 A 0 ACS_NamedExecuteAlways("PowerupSpawn", 0)
		CRON ABCDCBABCD 4 //A_SpawnItemEX("PowerupFX", frandom(-4.0, 4.0), frandom(-4.0, 4.0), 32.0 + frandom(-4.0, 4.0), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5))
		loop
		Pickup:
		TNT1 A 0 A_GiveInventory("Health", 100)
		TNT1 A 0 A_GiveInventory("PowerChronalDrive", 1)
		TNT1 A 0 ACS_NamedExecuteAlways("PrintPowerupMessage", 0)
		stop
		Death:
		TNT1 A 0 
		stop
	}
}
Actor BFG9000Replacer : AlphatiusPowerup replaces BFG9000 {}
Actor MegasphereReplacer : AlphatiusPowerup replaces Megasphere {}
Actor SoulsphereReplacer : AlphatiusPowerup replaces Soulsphere {}

//This actor also accelerates cooldowns in ALPHLIB.acs
Actor PowerChronalDrive : PowerInfiniteAmmo
{
	Powerup.Duration -15
	Powerup.Color "40 30 00"
}

Actor PowerupFX
{
	Radius 1
	Height 1
	RenderStyle ADD
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NONETID
	+BRIGHT
	Scale 0.2
	States
	{
		Spawn:
		TNT1 A 0 A_SetScale(scalex * 0.8, scaley * 0.8)
		ZAP7 ABCDEFGHIJKL 1 A_FadeOut(0.08)
		stop
	}
	
}